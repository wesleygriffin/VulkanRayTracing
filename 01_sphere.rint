#version 460 core
#extension GL_NV_ray_tracing : require

hitAttributeNV vec3 normalVector;

layout(shaderRecordNV) buffer Sphere {
  vec4 centerRadius;
} sphere;

void main() {
  const vec3 center = sphere.centerRadius.xyz;
  const float radius = sphere.centerRadius.w;

  vec3 oc = gl_WorldRayOriginNV - center;
  float a = dot(gl_WorldRayDirectionNV, gl_WorldRayDirectionNV);
  float b = dot(oc, gl_WorldRayDirectionNV);
  float c = dot(oc, oc) - (radius * radius);
  float d = b * b - a * c;

  if (d > 0.0) {
    float t1 = (-b - sqrt(d)) / a;
    float t2 = (-b + sqrt(d)) / a;

    if (gl_RayTminNV < t1 && t1 < gl_RayTmaxNV) {
      vec3 hitValue = gl_WorldRayOriginNV + gl_WorldRayDirectionNV * t1;
      normalVector = normalize((hitValue - center) / radius);
      reportIntersectionNV(t1, 0);
    } else if (gl_RayTminNV < t2 && t2 < gl_RayTmaxNV) {
      vec3 hitValue = gl_WorldRayOriginNV + gl_WorldRayDirectionNV * t2;
      normalVector = normalize((hitValue - center) / radius);
      reportIntersectionNV(t2, 0);
    }
  }
}