#version 460 core
#extension GL_NV_ray_tracing : require

layout(set = 0, binding = 0) uniform accelerationStructureNV topLevelAS;
layout(set = 0, binding = 1, rgba8) uniform image2D image;

layout(set = 0, binding = 2) uniform Camera {
  vec4 lowerLeft;
  vec4 horizontal;
  vec4 vertical;
  vec4 origin;
} camera;

layout(shaderRecordNV) buffer Sphere {
  vec4 centerRadius;
} sphere;

layout(location = 0) rayPayloadNV vec3 hitValue;

void main() {
  const vec2 uv = vec2(gl_LaunchIDNV.xy) / vec2(gl_LaunchSizeNV.xy);
  const vec3 direction = camera.lowerLeft.xyz +
    uv.x * camera.horizontal.xyz + uv.y * camera.vertical.xyz;

  uint rayFlags = gl_RayFlagsOpaqueNV;
  uint cullMask = 0xFF;
  float tmin = .001f;
  float tmax = 10000.f;

  traceNV(topLevelAS, rayFlags, cullMask, 0 /* sbtRecordOffset */,
    0 /* sbtRecordStride */, 0 /* missIndex */, camera.origin.xyz, tmin,
    direction, tmax, 0 /* payload */);

  imageStore(image, ivec2(gl_LaunchIDNV.xy), vec4(hitValue, 1.f));
}
